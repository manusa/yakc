import com.marcnuri.yakc.schema.api.ApiGeneratorTask
import com.marcnuri.yakc.schema.model.ModelGeneratorTask

apply from: rootDir.toPath().resolve('shared.gradle').toFile()

ext {
  isPublishable = true
  project.group = project.group + ".apis"
  pomName = 'Chaos Mesh Model & API'
  pomDescription = 'Retrofit2 based Java interfaces to create a declarative REST API client and model files for Chaos Mesh.'
}

dependencies {
  compile  project(':kubernetes-model')
  compileOnly "org.projectlombok:lombok:$version_projectlombok"
  annotationProcessor "org.projectlombok:lombok:$version_projectlombok"
}
sourceSets.main.java.srcDirs = ['src/api/java', 'src/main/java', 'src/model/java']


def templatesDirs = new File(rootProject.projectDir, "schema/templates")
def schemaDir = new File(projectDir, "schema")
def availableSchemaNames = Arrays.asList(
  "chaos-mesh-1.1.0.json"
)
def availableSchemas = availableSchemaNames.stream()
    .map { s -> new File(schemaDir, s) }
    .toArray{ i -> new File[i]}
task generateModel(type: ModelGeneratorTask) {
  group = 'generators'
  packageName = 'com.marcnuri.yakc'
  schemas = availableSchemas
  templatesDir = templatesDirs
  outputDirectory = projectDir
  skipGenerationRegexes = ['^io\\.k8s\\.apimachinery\\.pkg\\..*']
}
task generateApi(type: ApiGeneratorTask) {
  group = 'generators'
  packageName = 'com.marcnuri.yakc'
  schemas = availableSchemas
  templatesDir = templatesDirs
  outputDirectory = projectDir
  skipGenerationRegexes = ['^apis$', '^version$']
}