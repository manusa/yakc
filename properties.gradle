ext {
  isPublishable = false
  version_assertj = '3.15.0'
  version_bouncycastle = '1.65'
  version_jackson = '2.10.1' // Matches transitive version for retrofit2:converter-jackson:2.8.1
  version_junit = '5.4.2'
  version_openapi_generator = '4.3.0'
  version_projectlombok = '1.18.12'
  version_retrofit = '2.8.1'
  version_rxjava2 = '2.2.19'
}

project.group = 'com.marcnuri.yakc'
project.version = '0.0.0'

repositories {
  jcenter()
  mavenCentral()
}

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

ext.configurePublishing = { Project evaluatedProject ->
  if (!evaluatedProject.isPublishable) {
    return
  }
  apply plugin: 'maven'
  apply plugin: 'signing'
  task sourcesJar(type: Jar, dependsOn: classes) {
    group = 'maven'
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
  }
  task javadocJar(type: Jar, dependsOn: javadoc) {
    group = 'maven'
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
  }
  task assemblePublication(dependsOn: [classes, assemble, sourcesJar, javadocJar]) {
    group = 'build'
  }
  install.group 'maven'
  install.dependsOn assemblePublication
  signing {
    sign configurations.archives
  }
  artifacts {
    archives sourcesJar
    archives javadocJar
  }
  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }
        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }
        pom.project {
          name pomName
          description pomDescription
          url 'https://github.com/manusa/yakc'
          packaging 'jar'
          scm {
            connection 'scm:git:git://github.com/manusa/yakc.git'
            developerConnection 'scm:git:ssh://github.com:manusa/yakc.git'
            url 'https://github.com/manusa/yakc'
          }
          licenses {
            license {
              name 'The Apache License, Version 2.0'
              url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }
          developers {
            developer {
              name 'Marc Nuri'
              email 'marc@marcnuri.com'
            }
          }
        }
      }
    }
    uploadArchives.group 'maven'
    uploadArchives.dependsOn install
  }
}
