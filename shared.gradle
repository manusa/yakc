ext {
  isPublishable = false
  version_assertj = '3.15.0'
  version_bouncycastle = '1.65'
  version_jackson = '2.10.1' // Matches transitive version for retrofit2:converter-jackson:2.8.1
  version_junit = '5.4.2'
  version_mustache = '1.15'
  version_projectlombok = '1.18.12'
  version_retrofit = '2.8.1'
  version_rxjava2 = '2.2.19'
  version_swagger_parser = '2.0.19'
}

project.group = 'com.marcnuri.yakc'
project.version = '0.0.2'

repositories {
  jcenter()
  mavenCentral()
}

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}


ext.configurePublishing = { Project evaluatedProject ->
  if (!evaluatedProject.isPublishable || project.hasProperty("skipPublishing")) {
    return
  }
  apply plugin: 'maven'
  apply plugin: 'signing'
  def delombokedSourceDir = "$buildDir/delombok";
  task delombok(dependsOn: classes) {
    group = 'maven'
    inputs.dir projectDir.toPath().resolve("src").toFile()
    outputs.dir file(delombokedSourceDir)
    doLast {
      ant.taskdef(
        name: 'delombok',
        classname: 'lombok.delombok.ant.Tasks$Delombok',
        classpath: configurations.compile.asPath + ";" + configurations.compileOnly.asPath
      )
      ant.mkdir(dir: delombokedSourceDir)
      evaluatedProject.sourceSets.main.java.srcDirs.each { srcDir ->
        ant.delombok(verbose: 'true', encoding: 'UTF-8', to: delombokedSourceDir, from: srcDir)
      }
    }
  }
  task delombokedJavadoc(type: Javadoc, dependsOn: delombok) {
    group = 'maven'
    source = delombokedSourceDir
    classpath = files(configurations.compile, configurations.compileOnly.asPath)
  }
  task sourcesJar(type: Jar, dependsOn: classes) {
    group = 'maven'
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
  }
  task javadocJar(type: Jar, dependsOn: delombokedJavadoc) {
    group = 'maven'
    archiveClassifier.set('javadoc')
    from delombokedJavadoc.destinationDir
  }
  task assemblePublication(dependsOn: [classes, assemble, sourcesJar, javadocJar]) {
    group = 'maven'
  }
  install.group 'maven'
  install.dependsOn assemblePublication
  signing {
    sign configurations.archives
  }
  artifacts {
    archives sourcesJar
    archives javadocJar
  }
  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }
        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }
        pom.project {
          name pomName
          description pomDescription
          url 'https://github.com/manusa/yakc'
          packaging 'jar'
          scm {
            connection 'scm:git:git://github.com/manusa/yakc.git'
            developerConnection 'scm:git:ssh://github.com:manusa/yakc.git'
            url 'https://github.com/manusa/yakc'
          }
          licenses {
            license {
              name 'The Apache License, Version 2.0'
              url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }
          developers {
            developer {
              name 'Marc Nuri'
              email 'marc@marcnuri.com'
            }
          }
        }
      }
    }
    uploadArchives.group 'maven'
    uploadArchives.dependsOn install
  }
}
